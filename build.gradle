// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.7.21"
    ext.hilt_version = '2.43.2'
    ext.safe_args = '2.5.1'
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0'
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$safe_args"

        // NOTE: Do not place your application dependencies here; they belong in the individual module build.gradle files
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
    id "org.jetbrains.kotlinx.kover" version "0.6.1"
}

// Making sure that the Coverage task is executed before the SonarCube task.
tasks.sonarqube.dependsOn ":koverMergedReport"

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


// Sonar Cloud Setup
sonarqube {
    properties {
        property "sonar.projectKey", "Mithrandir21_mobile-multiplatform"
        property "sonar.organization", "mithrandir"
        property "sonar.host.url", "https://sonarcloud.io"

        def exclusionList = [
                // Res, AndroidManifest, etc
                "**/*.xml",

                // Excludes all DI packages
                "**/di/**/*"]

        property "sonar.coverage.exclusions", exclusionList

        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/kover/merged/xml/report.xml"
    }
}

// Kover Code Coverage setup
ext.getKoverExclusionList = {
    return ["*BR*",
            "*Args",
            "*Directions",
            "*BuildConfig",
            '*BuildConfig.*',
            '*.generated.callback.*',
            '*.di.*Module',
            '*.di.*Modules',
            '*.di.*Module.*',
            '*.di.*Modules.*',
            '*Hilt_*',
            '*Hilt*.*',
            '*_HiltModules*',
            "hilt_aggregated_deps.*",
            '*hilt_aggregated_deps*.*',
            '*DataBinderMapperImpl',
            '*DataBinderMapperImpl*',
            '*DataBindingTriggerClass',
            '*DataBindingTriggerClass.*',
            '*_*Factory',
            '*_Factory',
            '*_Factory*',
            '*_Provide*Factory',
            '*_MembersInjector',
            '*_MembersInjector.*',
            "*Fragment",
            "*Fragment\$*",
            "*Activity",
            "*Activity\$*",
            "*.databinding.*",
            "*AndroidManifest.xml",
            'dagger.hilt.internal.aggregatedroot.codegen',
            'dagger.hilt.internal.aggregatedroot.codegen.*',
            'dagger.hilt.internal.aggregatedroot.codegen._com_snappyshopper_SnappyApp']
}

koverMerged {
    enable()

    filters {
        classes {
            excludes.addAll(getKoverExclusionList())
        }
    }
}